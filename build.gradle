plugins {
    alias(libs.plugins.com.github.ben.manes.versions)
    alias(libs.plugins.org.cyclonedx.bom)
    alias(libs.plugins.org.sonarqube)
    alias(libs.plugins.org.springframework.boot)
}

ext {
    okhttpVersion = '5.3.2'
}

apply from: 'gradle/configureIntygBom.gradle'

springBoot {
    mainClass = 'se.inera.intyg.cts.CustomerTerminateServiceApplication'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'test-report-aggregation'

    group = 'se.inera.intyg.cts'
    version = System.properties['buildVersion'] ?: '0.0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        maven { url = "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral()
    }
}


subprojects {
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
        }
    }

    configurations {
        mockitoAgent
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-parameters")
    }

    dependencies {
        implementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        annotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testAnnotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        mockitoAgent platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        mockitoAgent("org.mockito:mockito-core") { transitive = false }
    }

    test {
        jvmArgs += [
                "-javaagent:${configurations.mockitoAgent.asPath}",
                "-Xshare:off"
        ]
    }

    testing {
        suites {
            test {
                useJUnitJupiter()
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }

    tasks.matching { it.name == "cyclonedxDirectBom" }.configureEach { t ->
        t.includeConfigs = ['^runtimeClasspath$']
    }
}

reporting {
    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-cts"
        property "sonar.projectName", "intyg-cts"
        property "sonar.exclusions", "**/testability/**,**/integrationtest/**"
    }
}